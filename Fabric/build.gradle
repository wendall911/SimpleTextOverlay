plugins {
    id 'multiloader-loader'
    id 'fabric-loom'
}

version = fabric_mod_version
archivesBaseName = "${mod_id}"

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings(loom.layered {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-${parchment_mc_version}:${parchment_version}@zip")
    })

    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"

    modImplementation "com.illusivesoulworks.spectrelib:spectrelib-fabric:${spectrelib_version}"
    include "com.illusivesoulworks.spectrelib:spectrelib-fabric:${spectrelib_version}"

    modImplementation "org.ladysnake.cardinal-components-api:cardinal-components-base:${cardinal_components_version}"
    modImplementation "org.ladysnake.cardinal-components-api:cardinal-components-entity:${cardinal_components_version}"
    include "org.ladysnake.cardinal-components-api:cardinal-components-base:${cardinal_components_version}"
    include "org.ladysnake.cardinal-components-api:cardinal-components-entity:${cardinal_components_version}"

    modRuntimeOnly("com.terraformersmc:modmenu:${mod_menu_version}") {
        transitive = false
    }

    modCompileOnly "curse.maven:fabric_seasons-413523:${fabric_seasons_version}"
    modCompileOnly "curse.maven:sereneseasons-291874:${serene_seasons_fabric_version}"
    modCompileOnly "curse.maven:glitchcore-955399:${glitch_core_fabric_version}"
    modRuntimeOnly "net.jodah:typetools:0.6.3"
    modImplementation "com.electronwill.night-config:core:3.6.7"

    include group: 'com.illusivesoulworks.spectrelib', name: 'spectrelib-fabric', version: "${spectrelib_version}"
}

loom {
    mixin {
        defaultRefmapName.set("${mod_id}.refmap.json")
    }
    runs {
        client {
            client()
            setConfigName("Fabric Client")
            ideConfigGenerated(true)
            runDir("run")
        }
        server {
            server()
            setConfigName("Fabric Server")
            ideConfigGenerated(true)
            runDir("run")
        }
    }
}

def changelogFile = project(":").projectDir.toString() + '/build/libs/changelog.txt'
def publishFile = project(":").projectDir.toString() + "/Fabric/build/libs/${archivesBaseName}-${version}.jar"

publishMods {
    file = file(publishFile)
    changelog = file(changelogFile).text
    type = "${release_type}".startsWith("release") ? STABLE : ("${release_type}".startsWith("beta") ? BETA : ALPHA)
    modLoaders.add('fabric')

    if (project.hasProperty('stocurseApiKey')) {
        curseforge {
            accessToken = project.stocurseApiKey
            projectId = project.stocurseId
            javaVersions.add(JavaVersion.toVersion("${java_version}"))
            clientRequired = true
            serverRequired = true
            minecraftVersions.addAll("${release_versions}".split(','))
            embeds('spectrelib')
			requires('fabric-api')
            optional('fabric-seasons')
            optional('serene-seasons')
        }
    }
    if (project.hasProperty('stoModrinthId')) {
        modrinth {
            accessToken = project.modrinthKey
            projectId = project.stoModrinthId
            minecraftVersions.addAll("${release_versions}".split(','))
            requires('fabric-api')
            optional('fabric-seasons')
            optional('serene-seasons')
        }
    }
    if (project.hasProperty('stoGithubToken')) {
    }
}
